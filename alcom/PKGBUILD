pkgname=alcom
pkgver=1.0.1
pkgrel=1
pkgdesc="Fast open-source alternative of VRChat Creator Companion."
arch=('x86_64')
url="https://github.com/vrc-get/vrc-get"
license=('MIT')
depends=(gtk3 webkit2gtk-4.1 openssl libsoup3 hicolor-icon-theme gcc-libs cairo gdk-pixbuf2 glibc glib2)
makedepends=(cargo cargo-about nodejs-lts-iron npm dotnet-sdk lld)
options=(!lto)
_archive="vrc-get-gui-v${pkgver}"
source=("$_archive.tar.gz::$url/archive/refs/tags/gui-v${pkgver}.tar.gz")
sha256sums=("SKIP")

prepare() {
  cd "$_archive"

  cd "vrc-get-gui"

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_archive/vrc-get-gui"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS="-C link-arg=-fuse-ld=lld"

  npm run -- tauri build --config '{"bundle":{"targets":["deb"]}}'
}

package() {
  cd "$_archive/vrc-get-gui"

  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -a "target/release/bundle/deb/ALCOM_${pkgver}_amd64/data/." "${pkgdir}"
}
